/*https://www.microchip.com/en-us/products/fpgas-and-plds/spld-cplds/pld-design-resources*/
Name     Alexsei Butiaev ;
PartNo   01 ;
Date     09.06.2022 ;
Revision 01 ;
Designer Engineer ;
Company  Home ;
Assembly None ;
Location Cyprus ;
Device   g16v8a ;

/* *************** INPUT PINS *********************/
PIN 1    =    A0; /* Input - CPU A0 */
PIN 2    =    A1; /* Input - CPU A1 */
PIN 3    =    A5; /* Input - CPU A5 */
PIN 4    =    A7; /* Input - CPU A7  */
PIN 5    =    A14; /* Input - CPU A14 */
PIN 6    =    A15; /* Input - CPU A15 */
PIN 7    =    NIORQ; /* Input - CPU !IORQ. Active Low */
PIN 8    =    NRD; /* Input - CPU !RD. Active Low */
PIN 9    =    NWR; /* Input - CPU !WR. Active Low */
PIN 11   =    NM1; /* Input - CPU !MI. Active Low */
PIN 12   =    NDI_ULA; /* Input - Disable ULA from EDGE connector. Active Low */
PIN 13   =    DI_32765; /* Input - Disable Memory segment Registor. Active Low */

/* *************** OUTPUT PINS *********************/
PIN 14    =  NIN254; /* Output - Read From Port 254 */
PIN 15    =  NOUT254; /* Output - Write To Port 254 */
PIN 16    =  CS_32765; /* Output - Write To Memory Segment Registor */
PIN 17    =  BC1; /* Output - Signal BC1 for AY-3-8910 */
PIN 18    =  BDIR; /* Output - Signal BC1 for AY-3-8910 */
PIN 19    =  NINKMST; /* Output - Read From Port 31 Kempston Joistic */

PORT254 = [A0, !A1, !A5, !A7, NIORQ, NDI_ULA]:#;

NIN254 = PORT254 # NRD;

NOUT254 = PORT254 # NWR;

CS_32765 = A0 & !A1 & A5 & A7 & A14 & !A15 & !NWR & !NIORQ & !DI_32765;

BC1 = !A1 & A5 & A7 & A14 & A15 & !NIORQ & NM1;

BDIR = !A1 & A5 & A7 & A15 & !NWR & !NIORQ;

NINKMST = !A1 # !A0 # A7 # A5 # NIORQ # NRD;
